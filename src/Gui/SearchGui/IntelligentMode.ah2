class IntelligentMode {
    ; static listView := unset
    ; static menu := unset
    static imgListID := 0
    static rowHeight := 0
    static imgPathToImgListIndex := Map()
    static hotkeyHandlerMap := Map()

    static pluginData := []
    static pluginSearchResult := []

    static startupFocusedRow => this.focusedRow - this.pluginSearchResult.Length

    ;#region èšç„¦è¡Œ
    static focusedRow
    {
        get => this.listView.GetNext()
        set {
            if (GlobalData.intelligentSearchResult.Length || this.pluginSearchResult.Length) { ; æœ‰å†…å®¹æ‰å¯ä¿®æ”¹
                if (value > GlobalData.intelligentSearchResult.Length + this.pluginSearchResult.Length)
                    value := 1
                else if (value == 0)
                    value := GlobalData.startupSearchResult.Length + this.pluginSearchResult.Length
                this.listView.Modify(value, "Select Focus Vis")
            }
        }
    }
    ;#endregion

    ;#region å¤±å»ç„¦ç‚¹è‡ªåŠ¨éšè—å¼€å…³
    static _autoHideFlag := 0
    static autoHideFlag {
        get => this._autoHideFlag
        set {
            _autoHideFunc() {
                if (!WinActive(SearchGui.gui))
                    IntelligentMode.hideGui(), SearchGui.hideTime := A_Now
            }
            SetTimer(_autoHideFunc, value ? 1000 : 0)
        }
    }
    ;#endregion

    static init() {
        ; æœ¬åˆ—è¡¨é»˜è®¤éšè—
        this.listView := SearchGui.gui.Add("ListView", "x10 y52 w560 r10 -Multi -hdr -E0x200 Hidden Count" 3, [""])
        this.listView.SetFont("s14 w700 c444444")

        this.loadImgs()

        ; é¼ æ ‡åŒå‡»è¿è¡Œ
        this.listView.OnEvent("DoubleClick", (_lv, rowNum) => this.intelligentRun(rowNum))
        ; èœå•æ 
        this.listView.OnEvent("ContextMenu", (_lv, rowNum, *) => !rowNum ?
            0 : rowNum > this.pluginSearchResult.Length ?
                this.menu.Show() : this.pluginSearchResult[rowNum].HasOwnProp("menu") ?
                    this.pluginSearchResult[rowNum].menu.Show() : 0
            )

        ; å¯åŠ¨æ¨¡å¼çƒ­é”®å¤„ç†å‡½æ•°
        this.hotkeyHandlerMap["~Esc"] := () => StrLen(SearchGui.edit.Value) ? SearchGui.searchText := SearchGui.edit.Value := "" : this.hideGui()
        this.hotkeyHandlerMap["Down"] := () => this.focusedRow++
        this.hotkeyHandlerMap["Up"] := () => this.focusedRow--

        this.menuInit()
    }

    ; æ™ºèƒ½æ¨¡å¼åˆ—è¡¨å³é”®èœå•åˆå§‹åŒ–
    static menuInit() {
        this.menu := Menu()
        this.menu.Add("æ‰§è¡Œé€‰ä¸­é¡¹`t(&R)", (*) => this.intelligentRun(this.focusedRow))
        this.menu.Add("ç¼–è¾‘é€‰ä¸­é¡¹`t(&E)", (*) => this.toIntelligentEditGui(this.startupFocusedRow))

        this.menu.SetIcon("1&", GlobalData.imgDir "\run.ico")
        this.menu.SetIcon("2&", GlobalData.imgDir "\edit.ico")
    }

    ; è·³è½¬è‡³æ™ºèƒ½æ¨¡å¼ç¼–è¾‘ç•Œé¢
    static toIntelligentEditGui(rowNum) {
        selectedItem := GlobalData.intelligentSearchResult[rowNum]
        group := selectedItem["group"]
        for index, item in GlobalData.intelligentData[group] {
            if (item == selectedItem) {
                posIndex := index
                break
            }
        }

        this.hideGui()
        IntelligentEditGui.showGui([group, posIndex])
    }

    ; run-withåˆ†ç»„ä¸‹çš„å¤„ç†å‡½æ•°
    static runWithHandler(item) {
        searchTxt := SearchGui.searchText
        if (item["match"]["mode"] == "reg") {
            reg := GlobalData.intelligentRegMap[item]
            searchTxt := RegExReplace(searchTxt, reg.exp, reg.replace, , 1)
        }

        if (item["script"]["mode"] == "arg") {    ; ä¼ é€’å•å‘½ä»¤è¡Œå‚æ•°
            arg := StrReplace(searchTxt, '"', '""')    ; åŒå¼•å·è½¬ä¹‰ä¸ºä¸¤ä¸ªåŒå¼•å·
            Run(Format('"{}" "{}"', item["script"]["program"], arg))
        }
        else if (item["script"]["mode"] == "args")    ; ä¼ é€’å¤šå‘½ä»¤è¡Œå‚æ•°ï¼Œä¸è¿›è¡Œè½¬ä¹‰ç­‰æ“ä½œ
            Run(Format('"{}" {}', item["script"]["program"], searchTxt))
        else    ; type: none
            Run(searchTxt)
    }

    ; searchåˆ†ç»„ä¸‹çš„å¤„ç†å‡½æ•°
    static searchHandler(item) {
        searchTxt := SearchGui.searchText

        if (item["match"]["mode"] == "reg") {
            reg := GlobalData.intelligentRegMap[item]
            searchTxt := RegExReplace(searchTxt, reg.exp, reg.replace, , 1)
        }

        Run(Format(item["url"], searchTxt))
    }

    ; æ ¹æ®é¡¹ç›®æ‰§è¡Œå¯¹åº”æ“ä½œ
    static intelligentRun(rowNum?) {
        static handler := Map("run-with", this.runWithHandler, "search", this.searchHandler)
        if (!IsSet(rowNum))
            rowNum := this.focusedRow
        if (rowNum > this.pluginSearchResult.Length) { ; æ‰§è¡Œé¡¹
            item := GlobalData.intelligentSearchResult[rowNum]
            if (handler.Has(item["group"]))
                handler[item["group"]](this, item)
            this.hideGui()
        } else if (rowNum > 0) { ; æ’ä»¶é¡¹
            plugin := this.pluginSearchResult[rowNum] ;å½“å‰æœç´¢ç»“æœå¯¹åº”çš„æ­£åˆ™
            content := RegExReplace(SearchGui.searchText, plugin.curReg[1], plugin.curReg[2], , 1)
            plugin.runHandler(content) ;ä¼ é€’æ­£åˆ™æ›¿æ¢åçš„ç»“æœ
        }
    }

    ; é‡æ–°åŠ è½½æœç´¢åˆ—è¡¨å›¾æ ‡èµ„æº
    static loadImgs(reloadIL := true) {
        SearchGui.placeholder := "æ­£åœ¨åŠ è½½æ™ºèƒ½æ¨¡å¼æœç´¢åˆ—è¡¨..."
        SearchGui.edit.Value := ""    ;æ¸…ç©ºæœç´¢æ¡†
        this.listView.Delete()    ;æ¸…ç©ºæ™ºèƒ½åˆ—è¡¨
        if (reloadIL) {    ; reloadILä¸ºfalseæ—¶æ˜¯ä¸ºäº†ä»…æ·»åŠ æ–°èµ„æº
            this.imgPathToImgListIndex := Map()    ;é‡ç½®å›¾ç‰‡è·¯å¾„ä¸åºå·æ˜ å°„
            oldImgListID := this.imgListID ? this.imgListID : 0 ;æ ‡è®°éœ€è¦åˆ é™¤æ—§å›¾ç‰‡åˆ—è¡¨
            this.imgListID := DllCall("ImageList_Create", "Int", 32, "Int", 32, "Int", 32, "Int", 1, "Int", 1)    ;åˆ›å»ºæ–°å›¾ç‰‡åˆ—è¡¨
            this.listView.SetImageList(this.imgListID, 1)

            ; è½½å…¥å›¾ç‰‡é”™è¯¯ã€æ ¹ç›®å½•ã€æ’ä»¶çš„å›¾æ ‡
            for name in ["\folder.png", "\noImg.png", "\plugin.ico"]
                this.imgPathToImgListIndex[GlobalData.imgDir name] := IL_Add(this.imgListID, GlobalData.imgDir name)
        }

        ; ä¸ºæ¯ä¸ªé¡¹ç›®æŒ‰éœ€è½½å…¥å›¾æ ‡
        for name in GlobalData.intelligentGroups {
            if (GlobalData.intelligentData.Has(name)) {
                for item in GlobalData.intelligentData[name] {
                    if (!this.imgPathToImgListIndex.Has(item["thumb"])) {    ; è‹¥å›¾æ ‡å°šæœªåŠ è½½
                        index := IL_Add(this.imgListID, GlobalData.customImgDir "\" item["thumb"])
                        if (index)    ; å›¾ç‰‡è½½å…¥æˆåŠŸ
                            this.imgPathToImgListIndex[item["thumb"]] := index
                    }
                }
            }
        }

        ; é‡è½½æ’ä»¶å›¾æ ‡
        this.pluginLoadImgs()

        if (reloadIL and oldImgListID)    ; æœ‰éœ€è¦é”€æ¯çš„å›¾ç‰‡åˆ—è¡¨
            DllCall("ImageList_Destroy", "Uint", oldImgListID)

        SearchGui.placeholder := "Hi, type something ğŸ˜ƒ"
    }

    ; ä»¥æ™ºèƒ½æ¨¡å¼æ˜¾ç¤ºgui
    static showGui(searchText := "") {
        if (SearchGui.hideTimeDiff()) {
            SearchGui.recoverGui()
            return
        }

        SearchGui.mode := 1
        SearchGui.edit.Value := SearchGui.searchText := searchText
        SearchGui.gui.Show()
        SearchGui.edit.Focus()
        IME_SET(0)
    }

    ; æ™ºèƒ½æ¨¡å¼ä¸‹éšè—ä¸»ç•Œé¢
    static hideGui() {
        this.autoHideFlag := 0
        SearchGui.gui.hide()
    }

    ; æœç´¢å³é‡ç½®åˆ—è¡¨æ˜¾ç¤ºåŒ¹é…çš„å†…å®¹
    static search() {
        static customSort(itemA, itemB) {
            if (itemA["match"]["mode"] != itemB["match"]["mode"]) {
                ; ä¼˜å…ˆçº§å¤§çš„åœ¨å‰
                return GlobalData.intelligentMatchPriority[itemB["match"]["mode"]] - GlobalData.intelligentMatchPriority[itemA["match"]["mode"]]
            } else if (itemA["match"]["mode"] == "reg") {
                ; æ¬¡åºå°çš„åœ¨å‰
                return GlobalData.intelligentRegMap[itemA].index - GlobalData.intelligentRegMap[itemB].index
            }
            return 0
        }


        keyword := SearchGui.searchText
        if (StrLen(keyword)) {
            GlobalData.intelligentSearchResult := []
            ; åŒ¹é…æ‰€æœ‰ç¬¦åˆé¡¹
            for name in GlobalData.intelligentGroups {
                if (GlobalData.intelligentData.Has(name)) {
                    for item in GlobalData.intelligentData[name] {
                        if (!item["enabled"])
                            continue
                        if (item["match"]["mode"] == "str")
                            GlobalData.intelligentSearchResult.Push(item)
                        else if (item["match"]["mode"] == "reg") {
                            for index, exp in item["match"]["exp"]
                                if RegExMatch(keyword, exp) {
                                    GlobalData.intelligentRegMap[item] := { exp: exp, replace: item["match"]["replace"][index], index: index }
                                    GlobalData.intelligentSearchResult.Push(item)
                                    break
                                }
                        }
                    }
                }
            }
            ; æŒ‰æ‹Ÿå®šçš„ä¼˜å…ˆçº§é™åºæ’åˆ—
            QuickSort(GlobalData.intelligentSearchResult, customSort)
        } else    ; æœç´¢è¯ä¸ºç©ºä»€ä¹ˆéƒ½ä¸æ˜¾ç¤º
            GlobalData.intelligentSearchResult := []

        ;æ˜¾ç¤ºæ–°åˆ—è¡¨
        this.listView.Opt("-Redraw")    ;ç¦ç”¨é‡ç»˜
        this.listView.Delete()
        ;æ’ä»¶æœç´¢å¹¶æ·»åŠ æ’ä»¶é¡¹åˆ°åˆ—è¡¨
        this.pluginSearchAndAdd()
        ; æ·»åŠ æ‰§è¡Œé¡¹åˆ°åˆ—è¡¨
        for item in GlobalData.intelligentSearchResult {
            if (this.imgPathToImgListIndex.Has(item["thumb"]))
                this.listView.Add("Icon" this.imgPathToImgListIndex[item["thumb"]], "  " item["title"])
            else    ; æ— å›¾æ ‡ ä½¿ç”¨ç‰¹æ®Šè·¯å¾„
                this.listView.Add("Icon" this.imgPathToImgListIndex[GlobalData.imgDir "\noImg.png"], "  " item["title"])
        }

        this.resizeGui()
    }

    ;é‡ç½®listViewå’Œguié«˜åº¦
    static resizeGui() {
        len := GlobalData.intelligentSearchResult.Length + this.pluginSearchResult.Length
        if (len && this.rowHeight == 0) {
            LV_XYstruct := Buffer(16, 0)
            SendMessage(0x1038, 1, LV_XYstruct.Ptr, , this.listView)
            if (rowH := NumGet(LV_XYstruct, 12, "UInt") - NumGet(LV_XYstruct, 4, "UInt"))
                this.rowHeight := rowH
        }

        LVHeight := len > 0 ? Ceil((len >= 8 ? 8 : len) * this.rowHeight) : 0
        ControlMove(, , , LVHeight, this.listView) ; ä½¿ç”¨ControlMoveé¿å…DPI ç¼©æ”¾
        this.listView.ModifyCol(1, "530")
        this.focusedRow := 1    ;èšç„¦ç¬¬ä¸€è¡Œ
        this.listView.Opt("Redraw")
        WinMove(, , 550 * (A_ScreenDPI / 96), LVHeight + Ceil(55 * (A_ScreenDPI / 96)), SearchGui.gui)
    }

    ; è½½å…¥æ’ä»¶å›¾ç‰‡
    static pluginLoadImgs() {
        ; ä¸ºæ’ä»¶é¡¹ç›®è½½å…¥å›¾æ ‡
        for i, plugin in this.pluginData {
            hIcon := plugin.HasOwnProp("hIcon") ? plugin.hIcon : PluginHelper.getPluginHIcon(plugin.name)
            plugin.index := i ;ä¸ºæ¯ä¸ªæ’ä»¶é¡¹ç¼–å·
            if (hIcon && index := IL_Add(this.imgListID, "HICON:*" hIcon))
                this.imgPathToImgListIndex[Format("Plugin:{}:{}", plugin.name, plugin.index)] := index
            ; å¦åˆ™åœ¨searchæ—¶ä½¿ç”¨é»˜è®¤å›¾ç‰‡
        }
    }

    ; æ’ä»¶æœç´¢å¹¶æ·»åŠ æ’ä»¶é¡¹åˆ°åˆ—è¡¨
    static pluginSearchAndAdd() {
        keyword := SearchGui.searchText
        if (StrLen(keyword)) {
            this.pluginSearchResult := []
            ; åŒ¹é…æ‰€æœ‰ç¬¦åˆé¡¹
            for item in this.pluginData {
                for index, reg in item.reg
                    if RegExMatch(keyword, reg[1]) {
                        item.curReg := reg
                        item.regIndex := index
                        this.pluginSearchResult.Push(item)
                        break
                    }
            }
        } else    ; æœç´¢è¯ä¸ºç©ºä»€ä¹ˆéƒ½ä¸æ˜¾ç¤º
            this.pluginSearchResult := []

        ; æŒ‰regIndexä»å°åˆ°å¤§
        QuickSort(this.pluginSearchResult, (itemA, itemB) => itemA.regIndex - itemB.regIndex)

        ; æ·»åŠ åˆ°æœç´¢åˆ—è¡¨
        for item in this.pluginSearchResult {
            path := Format("Plugin:{}:{}", item.name, item.index)
            if (!this.imgPathToImgListIndex.Has(path))    ; å›¾æ ‡è½½å…¥å¤±è´¥åˆ™ä½¿ç”¨é»˜è®¤å›¾æ ‡
                this.listView.Add("Icon" this.imgPathToImgListIndex[GlobalData.imgDir "\plugin.ico"], "  " item.title)
            else
                this.listView.Add("Icon" this.imgPathToImgListIndex[path], "  " item.title)
        }
    }
}