class SearchGui {
    ; static gui := unset
    ; static edit := unset
    ; static thumbList := unset
    ; static thumb := unset

    static hideTime := 0

    ;#region è®¾ç½®æœç´¢æ¡†å ä½ç¬¦
    static placeholder {
        set => DllCall("User32.dll\SendMessageW", "Ptr", SearchGui.edit.Hwnd, "Uint", (0x1500 + 1), "Ptr", True, "WStr", value)
    }
    ;#endregion

    ;#region è®¾ç½®æœç´¢æ¨¡å¼ 0: å¯åŠ¨æ¨¡å¼, 1: æ™ºèƒ½æ¨¡å¼ ,-1: æ’ä»¶æ¨¡å¼
    static _mode := -2
    static mode {
        get => this._mode
        set {
            if (this._mode == value)    ; ä¸éœ€è¦åˆ‡æ¢æ—¶ç›´æ¥è¿”å›
                return
            this._mode := value
            if (this._mode == 1) {    ; IntelligentMode
                this.thumb.Value := "hBitmap:*" this.thumbList[2]
                this.placeholder := "Hi, type something ğŸ˜ƒ"
                for c in [StartupMode, PluginMode]
                    c.autoHideFlag := c.listView.Visible := false
                IntelligentMode.listView.Visible := true

                this.searchText := this.searchText    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
                IntelligentMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—

            } else if (this._mode == 0) {    ; StartupMode
                this.thumb.Value := "hBitmap:*" this.thumbList[1]
                this.placeholder := "Hi, Starter"
                for c in [IntelligentMode, PluginMode]
                    c.autoHideFlag := c.listView.Visible := false
                StartupMode.listView.Visible := true

                this.searchText := this.searchText    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
                StartupMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—
            } else { ; PluginMode
                ; æ ¹æ®å›¾åƒå¥æŸ„ç±»å‹è½½å…¥
                If (DllCall("GetObjectType", "Ptr", PluginMode.thumb) = 7) ;  OBJ_BITMAP = 7
                    this.thumb.Value := "hBitmap:*" PluginMode.thumb
                else
                    this.thumb.Value := "hIcon:*" PluginMode.thumb

                for c in [IntelligentMode, StartupMode]
                    c.autoHideFlag := c.listView.Visible := false
                PluginMode.listView.Visible := true
                PluginMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—
                ; searchTextçš„è°ƒæ•´ç•™ç»™åˆ‡æ¢ç¨‹åº
            }
        }
    }
    ;#endregion

    ;#region æœç´¢å†…å®¹
    static _searchText := ""
    static searchText {
        get => this._searchText
        set {
            this._searchText := Value
            if (this.mode == 1) {
                IntelligentMode.search()
            } else if (this.mode == 0) {
                StartupMode.search()
            } else {
                PluginMode.searchHandler(Value)
            }
        }
    }
    ;#endregion

    ; åˆå§‹åŒ–
    static init() {
        this.gui := Gui("Owner AlwaysOnTop -Resize -Caption", "Starter")
        this.gui.BackColor := "FFFFFF"
        this.gui.SetFont("s20 q5 c333333", "NSimSun")
        this.gui.SetFont(, "é›…ç—-ç®€")    ;ä¼˜å…ˆä½¿ç”¨æ›´å¥½çœ‹çš„å­—ä½“
        this.gui.MarginY := 0

        this.edit := this.gui.AddEdit("x10 y10 h30 w445 -E0x200")
        this.placeholder := "Hi, Starter"
        this.edit.Focus()

        search(edit, info) {    ;é˜²æŠ–
            static f := (*) => this.searchText := this.edit.Value
            SetTimer(f, 0)
            SetTimer(f, -50)
        }
        this.edit.OnEvent("Change", search)    ; æ–‡æœ¬å˜åŠ¨äº‹ä»¶
        this.gui.OnEvent("DropFiles", (g, c, fileList, *) => this.dropAddItem(fileList))    ; æ–‡ä»¶æ‹–å…¥äº‹ä»¶


        this.thumbList := [LoadPicture(GlobalData.imgDir "\Starter.png", "w64"), LoadPicture(GlobalData.imgDir "\AI.png", "w64")]
        this.thumb := this.gui.AddPicture("x460 y10 w30 h30", "hBitmap:*" this.thumbList[1])

        ; å›è½¦è§¦å‘
        this.gui.AddButton("Default Hidden").OnEvent("Click", (*) => this.mode == 0 ?
            StartupMode.startupRun() : this.mode == 1 ?
                IntelligentMode.intelligentRun() : PluginMode.pluginRun())

        this.gui.Show("Hide y" Round(A_ScreenHeight / 5))
        StartupMode.init()
        IntelligentMode.init()
        PluginMode.init()

        this.hotkeyInit()

        this.hideTime := DateAdd(A_Now, -10, "m") ; ä¿è¯åˆå§‹hideTimeè¶³å¤Ÿæ—©
    }

    ; æ‹–æ‹½æ·»åŠ 
    static dropAddItem(fileList) {
        for filePath in fileList {
            SplitPath(filePath, , , , &fileNameNoExt)
            GlobalData.startupData.Push([filePath, filePath, 0, fileNameNoExt, ChineseFirstChar(fileNameNoExt)])
        }

        StartupMode.loadImgs(false)    ;è½½å…¥æ–°å›¾ç‰‡ï¼Œä¸é‡æ–°åŠ è½½å…¶ä»–å›¾ç‰‡

        if (SearchGui.mode !== 0)    ; éå¯åŠ¨æ¨¡å¼ä¸‹ï¼Œè½¬ä¸ºå¯åŠ¨æ¨¡å¼
            SearchGui.mode := 0    ; è½¬æ¢å¯åŠ¨æ¨¡å¼æ—¶ä¼šæœç´¢ä¸€æ¬¡
        else
            StartupMode.search()
    }

    ; çƒ­é”®åˆå§‹åŒ–
    static hotkeyInit() {
        capsLockHandler(key) {
            if (KeyWait("CapsLock", "T0.5")) {
                if (A_ThisHotkey = A_PriorHotkey and A_TimeSincePriorHotkey < 300) {
                    StartupMode.showGui()
                }
            } else {
                IntelligentMode.showGui(GetSelectedText())
                KeyWait("CapsLock")    ;ç­‰å¾…æ¾å¼€
            }
        }

        Hotkey("~CapsLock", capsLockHandler, "Off") ;å…ˆä¸å¼€å¯çƒ­é”®ï¼Œç­‰åŠ è½½å®Œæ¯•åå†å¼€å¯

        ; æ ¹æ®æ¨¡å¼å’Œçƒ­é”®å†…å®¹æ‰§è¡Œ
        hotkeyHandler(key) {
            if (this.mode == 1) {
                if (IntelligentMode.hotkeyHandlerMap.Has(key))
                    IntelligentMode.hotkeyHandlerMap[key]()
            } else if (this.mode == 0) {
                if (StartupMode.hotkeyHandlerMap.Has(key))
                    StartupMode.hotkeyHandlerMap[key]()
            } else {
                if (PluginMode.hotkeyHandlerMap.Has(key))
                    PluginMode.hotkeyHandlerMap[key]()
            }
        }

        ;===ä»…å½“çª—å£æ¿€æ´»æ—¶æœ‰æ•ˆ===
        HotIfWinActive("ahk_id " this.gui.Hwnd)

        Hotkey("Tab Up", key => this.mode >= 0 ? this.mode := !this.mode : 0) ;0,1æ¨¡å¼ä¸‹äº’ç›¸åˆ‡æ¢,-1æ¨¡å¼ä¸‹å¿½ç•¥
        ; æ¸…ç©ºã€éšè—
        Hotkey("~Esc", hotkeyHandler)
        ; æ–‡ä»¶å¤¹ä¸­æ˜¾ç¤º
        Hotkey("~Left Up", hotkeyHandler)
        ; ä¸Šä¸‹ç§»åŠ¨ç„¦ç‚¹è¡Œ
        Hotkey("Down", hotkeyHandler)
        Hotkey("Up", hotkeyHandler)
        HotIf()
    }

    ; éšè—æ—¶é—´æ˜¯å¦åœ¨æ—¶é™
    static hideTimeDiff() {
        return DateDiff(A_Now, SearchGui.hideTime, "s") < 15
    }

    static recoverGui() {
        if (this.mode == 0) {
            StartupMode.autoHideFlag := 1
        } else if (this.mode == 1) {
            IntelligentMode.autoHideFlag := 1
        } else {
            PluginMode.autoHideFlag := 1
        }
        this.gui.Show()
        this.edit.Focus()
        IME_SET(0)
    }

    ; æ ¹æ®æ¨¡å¼éšè—ä¸»ç•Œé¢
    static hideGui() {
        if (this.mode == 0) {
            StartupMode.hideGui()
        } else if (this.mode == 1) {
            IntelligentMode.hideGui()
        } else {
            PluginMode.hideGui()
        }
    }
}