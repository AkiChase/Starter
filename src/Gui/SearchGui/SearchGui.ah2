class SearchGui {
    ; static gui := unset
    ; static edit := unset
    ; static thumbList := unset
    ; static thumb := unset
    ; static contentThumb := unset

    static noRecover := true
    static hideTime := A_Now

    ;#region è®¾ç½®æœç´¢æ¡†å ä½ç¬¦
    static _placeholder := ""
    static placeholder {
        get => this._placeholder
        set {
            if (this._placeholder != Value) {
                this._placeholder := Value
                EditCtrlFunc.setPlaceholder(SearchGui.edit, Value)
            }
        }
    }
    ;#endregion

    ;#region è®¾ç½®æœç´¢æ¨¡å¼ -1: æ’ä»¶æ¨¡å¼,0: å¯åŠ¨æ¨¡å¼, 1: æ™ºèƒ½æ¨¡å¼
    static _mode := -2
    static mode {
        get => this._mode
        set {
            if (this._mode == Value) {  ; ä¸éœ€è¦åˆ‡æ¢æ—¶,å¼ºåˆ¶æ¿€æ´»æŒ‡å®šçš„autoHide
                l := [PluginMode, StartupMode, IntelligentMode]
                curModeC := l[Value + 2], l.RemoveAt(Value + 2)
                curModeC.autoHideFlag := true
                for c in l
                    c.autoHideFlag := false
                return
            }
            ;å°†åˆ‡æ¢å¯åŠ¨æ¨¡å¼ä¸”å­˜åœ¨éæ–‡æœ¬pastedContentæ—¶ï¼Œåˆ‡æ¢å‰è¦æ¸…é™¤pastedContent
            if (Value = 0 && this._pastedContentType != "text")
                this.clearPastedContent()

            ;å°†åˆ‡æ¢å¯åŠ¨æ¨¡å¼ä¸”çª—å£åŒ¹é…æ¨¡å¼å¼€å¯æ—¶ï¼Œåˆ‡æ¢å‰è¦å…³é—­çª—å£åŒ¹é…æ¨¡å¼
            if (Value = 0 && this.winInfoMatchFlag)
                this.winInfoMatchFlag := false

            this._mode := Value
            if (this._mode == 1) {    ; IntelligentMode
                this.thumb.Value := "hBitmap:*" this.thumbList[2]
                this.placeholder := IntelligentMode.defaultPlaceholder
                for c in [StartupMode, PluginMode]
                    c.autoHideFlag := c.listView.Visible := false
                IntelligentMode.listView.Visible := true

                this.searchText := this.searchText    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
                IntelligentMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—

            } else if (this._mode == 0) {    ; StartupMode
                this.thumb.Value := "hBitmap:*" this.thumbList[1]
                this.placeholder := StartupMode.defaultPlaceholder
                for c in [IntelligentMode, PluginMode]
                    c.autoHideFlag := c.listView.Visible := false
                StartupMode.listView.Visible := true

                this.searchText := this.searchText    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
                StartupMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—
            } else { ; PluginMode
                ; æ ¹æ®å›¾åƒå¥æŸ„ç±»å‹è½½å…¥
                If (DllCall("GetObjectType", "Ptr", PluginMode.thumb) = 7) ;  OBJ_BITMAP = 7
                    this.thumb.Value := "hBitmap:*" PluginMode.thumb
                else
                    this.thumb.Value := "hIcon:*" PluginMode.thumb

                for c in [IntelligentMode, StartupMode]
                    c.autoHideFlag := c.listView.Visible := false
                PluginMode.listView.Visible := true
                PluginMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—
                ; searchTextçš„è°ƒæ•´ç•™ç»™åˆ‡æ¢ç¨‹åº
            }
        }
    }
    ;#endregion

    ;#region æœç´¢å†…å®¹
    static _searchText := ""
    static searchText {
        get => this._searchText
        set {
            this._searchText := Value
            if (this.mode == 1) {
                IntelligentMode.search()
            } else if (this.mode == 0) {
                StartupMode.search()
            } else {
                PluginMode.searchHandler(Value)
            }
        }
    }
    ;#endregion

    ;#region ç²˜è´´å†…å®¹
    static clearPastedContent := (this) => (this._pastedContentType := "text", this.pastedContent := "")
    static _pastedContentType := "text"
    static _pastedContent := ""
    static pastedContent {
        get => this._pastedContent
        set {
            if (this._pastedContentType = "bitmap"
                && DllCall("OpenClipboard", "ptr", 0) && DllCall("IsClipboardFormatAvailable", "uint", 2)) {
                ; ä½å›¾
                hBitmap := DllCall("GetClipboardData", "uint", 2, "ptr") ; æŒ‡å‘å‰ªåˆ‡æ¿å†…æ•°æ®
                hBitmap := LoadPicture("HBITMAP:*" hBitmap) ;å¤åˆ¶ä¸€ä»½
                DllCall("CloseClipboard")

                this.placeholder := "", this.edit.Move(50, , 455)
                this.contentThumb.Value := "HBITMAP:*" hBitmap, this.contentThumb.Visible := true
                this.placeholder := "å›¾ç‰‡", this._pastedContent := hBitmap
            } else if (this._pastedContentType = "file") {
                ; æ–‡ä»¶
                fileList := Type(Value) == "Array" ? Value : StrSplit(Value, "`r`n") ; è‡ªåŠ¨å¤„ç†æ•°ç»„æˆ–å­—ç¬¦ä¸²ç±»å‹

                hIcon := SubStr(fileList[1], InStr(fileList[1], ".", , , -1) + 1) = "exe" ?
                    LoadPicture(fileList[1], "icon1", &_) : PluginHelper.Utils.associatedHIcon(fileList[1])
                this.placeholder := "", this.edit.Move(50, , 455)
                this.contentThumb.Value := "HICON:" hIcon, this.contentThumb.Visible := true
                this.placeholder := "æ–‡ä»¶", this._pastedContent := fileList
                if (fileList.Length > 1)
                    this.showFileListInfo(fileList)
            } else {
                ; è§†ä½œæ–‡æœ¬
                this._pastedContentType := "text"
                this.placeholder := "", this.edit.Move(10, , 495)
                this.contentThumb.Visible := false, this._pastedContent := Value
                this.placeholder := [PluginMode, StartupMode, IntelligentMode][this.mode + 2].defaultPlaceholder
            }
        }
    }
    ;#endregion

    ; é•¿æŒ‰è·å–çš„å½“å‰çª—å£ä¿¡æ¯
    static workWinInfo := {
        hwnd: 0,
        title: "",
        class: "",
        processPath: "",
        processName: ""
    }

    ;#region æ˜¯å¦å¤„äºçª—å£åŒ¹é…
    static _winInfoMatchFlag := false
    static winInfoMatchFlag {
        get => this._winInfoMatchFlag
        set => this._winInfoMatchFlag := Value
    }
    ;#endregion

    ; åˆå§‹åŒ–
    static init() {
        this.gui := Gui("Owner AlwaysOnTop -Resize -Caption", "Starter")
        this.gui.BackColor := "FFFFFF"
        this.gui.SetFont("s20 q5 c333333", "NSimSun")
        this.gui.SetFont(, "é›…ç—-ç®€")    ;ä¼˜å…ˆä½¿ç”¨æ›´å¥½çœ‹çš„å­—ä½“
        this.gui.MarginY := 0

        this.edit := this.gui.AddEdit("x10 y10 h30 w495 -E0x200")
        this.edit.OnEvent("ContextMenu", (*) => 0)

        search(edit, info) {    ;é˜²æŠ–
            static f := (*) => this.searchText := this.edit.Value
            SetTimer(f, 0)
            SetTimer(f, -50)
        }
        this.edit.OnEvent("Change", search)    ; æ–‡æœ¬å˜åŠ¨äº‹ä»¶
        this.gui.OnEvent("DropFiles", (g, c, fileList, *) => this.dropAddItem(fileList))    ; æ–‡ä»¶æ‹–å…¥äº‹ä»¶


        this.thumbList := [LoadPicture(GlobalData.imgDir "\Starter.png", "w64"), LoadPicture(GlobalData.imgDir "\AI.png", "w64")]
        this.thumb := this.gui.AddPicture("x510 y10 w30 h30", "hBitmap:*" this.thumbList[1])
        this.thumb.OnEvent("Click", (*) => this.winInfoMatchFlag ? this.showWinInfo() : 0)

        this.contentThumb := this.gui.AddPicture("x7 y7 w36 h36 +Border +Hidden")

        this.contentThumb.OnEvent("Click", (*) => this._pastedContentType = "file" ? this.showFileListInfo(this.pastedContent) : 0)

        ; å›è½¦è§¦å‘
        this.gui.AddButton("Default Hidden").OnEvent("Click", (*) => this.mode == 0 ?
            StartupMode.startupRun() : this.mode == 1 ?
                IntelligentMode.intelligentRun() : PluginMode.pluginRun())

        this.gui.Show("w550 Hide y" Ceil(A_ScreenHeight / 5))
        StartupMode.init()
        IntelligentMode.init()
        PluginMode.init()

        this.hotkeyInit()
    }

    ; æ‹–å…¥æ·»åŠ 
    static dropAddItem(fileList) {
        if (SearchGui.mode == 0) {
            for filePath in fileList {
                SplitPath(filePath, , , , &fileNameNoExt)
                GlobalData.startupData.Push([filePath, filePath, 0, fileNameNoExt, ChineseFirstChar(fileNameNoExt)])
            }
            StartupMode.loadImgs(false)    ;è½½å…¥æ–°å›¾ç‰‡ï¼Œä¸é‡æ–°åŠ è½½å…¶ä»–å›¾ç‰‡
            StartupMode.search()
            Tip.show("æ‹–å…¥æ·»åŠ å¯åŠ¨é¡¹", "æ·»åŠ æˆåŠŸ, æ–°å¢ " fileList.Length " ä¸ªå¯åŠ¨é¡¹", 2000)
        } else if (SearchGui.mode == 1) {
            SearchGui._pastedContentType := "file"
            SearchGui.pastedContent := fileList
            SearchGui.searchText := SearchGui.searchText ; ä¸ä¿®æ”¹å†…å®¹ï¼Œç›´æ¥è§¦å‘æœç´¢
        } else if (SearchGui.mode == -1)
            PluginMode.onDropFiles(fileList)
    }

    ; çƒ­é”®åˆå§‹åŒ–
    static hotkeyInit() {
        capsLockHandler(key) {
            if (KeyWait("CapsLock", "T0.4")) { ; éé•¿æŒ‰
                if (A_ThisHotkey = A_PriorHotkey && A_TimeSincePriorHotkey < 300) {
                    StartupMode.showGui() ; æ˜¾ç¤ºå¹¶æ¸…ç©ºæœç´¢æ¡†
                }
            } else { ; é•¿æŒ‰0.4ç§’
                clipSaved := ClipboardAll()
                content := GetSelectedText(0.1, &outType) ; ç­‰å¾…æœ€å¤š0.1så‰ªåˆ‡æ¿

                ; è·å–çª—å£ä¿¡æ¯
                this.workWinInfo.hwnd := WinExist("A")
                this.workWinInfo.class := WinGetClass()
                this.workWinInfo.processPath := WinGetProcessPath()
                this.workWinInfo.processName := WinGetProcessName()
                this.workWinInfo.title := WinGetTitle()

                ; æ˜¯å¦è¿›å…¥çª—å£åŒ¹é…æ¨¡å¼
                if (!content && (outType = 'text' || outType = "timeout")) { ;
                    this.winInfoMatchFlag := true ; å¼€å¯çª—å£åŒ¹é…æ¨¡å¼
                } else
                    this.winInfoMatchFlag := false ; å…³é—­çª—å£åŒ¹é…æ¨¡å¼

                this.noRecover := true ; ä¿è¯èƒ½æ˜¾ç¤ºæ™ºèƒ½æ¨¡å¼
                IntelligentMode.showGui(outType = 'text' ? content : "")  ; æ˜¾ç¤ºå¹¶è§†æƒ…å†µå¡«å…¥æœç´¢æ¡†

                if (this.winInfoMatchFlag) {
                    this.placeholder := "Hi, Window Match ğŸ’»"
                    this.showWinInfo()
                }


                if (outType != 'timeout') {
                    this._pastedContentType := outType
                    this.pastedContent := content
                }
                this.searchText := "" ; è§¦å‘æœç´¢
                A_Clipboard := clipSaved
                KeyWait("CapsLock")    ;ç­‰å¾…æ¾å¼€, é¿å…é‡å¤è°ƒç”¨
            }
        }

        Hotkey("~CapsLock", capsLockHandler, "Off") ;å…ˆä¸å¼€å¯çƒ­é”®ï¼Œç­‰åŠ è½½å®Œæ¯•åå†å¼€å¯

        ; æ ¹æ®æ¨¡å¼å’Œçƒ­é”®å†…å®¹æ‰§è¡Œ
        hotkeyHandler(key) {
            if (this.mode == 1) {
                if (IntelligentMode.hotkeyHandlerMap.Has(key))
                    IntelligentMode.hotkeyHandlerMap[key]()
            } else if (this.mode == 0) {
                if (StartupMode.hotkeyHandlerMap.Has(key))
                    StartupMode.hotkeyHandlerMap[key]()
            } else {
                if (PluginMode.hotkeyHandlerMap.Has(key))
                    PluginMode.hotkeyHandlerMap[key]()
            }
        }

        ;===ä»…å½“çª—å£æ¿€æ´»æ—¶æœ‰æ•ˆ===
        HotIfWinActive("ahk_id " this.gui.Hwnd)

        Hotkey("Tab Up", key => this.mode >= 0 ? this.mode := !this.mode : 0) ;0,1æ¨¡å¼ä¸‹äº’ç›¸åˆ‡æ¢,-1æ¨¡å¼ä¸‹å¿½ç•¥
        ; æ¸…ç©ºã€éšè—
        Hotkey("~Esc", hotkeyHandler)
        ; æ–‡ä»¶å¤¹ä¸­æ˜¾ç¤º
        Hotkey("~Right Up", hotkeyHandler)
        ; ä¸Šä¸‹ç§»åŠ¨ç„¦ç‚¹è¡Œ
        Hotkey("Down", hotkeyHandler)
        Hotkey("Up", hotkeyHandler)
        Hotkey("$^V", hotkeyHandler)

        ; Alt+æ•°å­—å¿«æ·é€‰ä¸­
        altHandler(key) {
            i := Integer(SubStr(key, 2))
            ; LVM_GETTOPINDEX := 0x1027
            LV := [PluginMode, StartupMode, IntelligentMode][this.mode + 2].listView
            topIndex := SendMessage(0x1027, 0, 0, , LV)
            if (topIndex + i <= LV.GetCount())
                LV.Modify(topIndex + i, "Select Focus Vis")
        }
        loop 10
            Hotkey("!" A_Index - 1, altHandler)
        HotIf()
    }

    ; æ˜¾ç¤ºçª—å£åŒ¹é…ä¿¡æ¯
    static showWinInfo() {
        WiseGuiHelper.WiseGui("workWinInfo", "Main: 0", "Theme: ,,,0", "FontSub: s14 ,é›…ç—-ç®€",
            "Shadow: 1", "TextWidth: 530", "Transparency: 192", "Timer: 3000",
            "SubText: " Format("@å·¥ä½œçª—å£æ ‡é¢˜`n{}`n@å·¥ä½œçª—å£ç±»`n{}`n@å·¥ä½œçª—å£ç¨‹åº`n{}"
                , this.workWinInfo.title, this.workWinInfo.class, this.workWinInfo.processName)
        )
        if (WinExist("WiseGui\workWinInfo ahk_class AutoHotkeyGUI")) {
            WinGetPos(&x, &y, , , this.gui)
            WinGetPos(, , , &h)
            WinMove(x, y - h - 10)
        }
    }

    ; æ˜¾ç¤ºæ–‡ä»¶åˆ—è¡¨ä¿¡æ¯
    static showFileListInfo(fileList) {
        loop 3 {
            if (fileList.Has(A_Index))
                msg .= fileList[A_Index] "`n`n"
            else
                break
        }
        if (fileList.Has(4))
            msg .= "......"
        Tip.show("å…±" fileList.Length "ä¸ªæ–‡ä»¶", msg, 4000)
    }

    ; éšè—æ—¶é—´æ˜¯å¦åœ¨æ—¶é™
    static hideTimeDiff() {
        return DateDiff(A_Now, SearchGui.hideTime, "s") < 15
    }

    ; æ¢å¤çª—å£
    static recoverGui() {
        if (this.mode == 0) {
            StartupMode.autoHideFlag := 1
        } else if (this.mode == 1) {
            IntelligentMode.autoHideFlag := 1
        } else {
            PluginMode.autoHideFlag := 1
        }
        this.gui.Show()
        this.edit.Focus()
        IME_SET(0)
    }

    ; æ ¹æ®æ¨¡å¼éšè—ä¸»ç•Œé¢
    static hideGui(recordHideTime := false) {
        if (this.mode == 0) {
            StartupMode.hideGui()
        } else if (this.mode == 1) {
            IntelligentMode.hideGui()
        } else {
            PluginMode.hideGui()
        }
        if (recordHideTime) ;è®°å½•å…³é—­æ—¶é—´ï¼Œ15så†…å¯æ¢å¤
            SearchGui.hideTime := A_Now
    }
}