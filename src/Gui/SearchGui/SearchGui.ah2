class SearchGui {
    ; static gui := unset
    ; static edit := unset
    ; static thumbList := unset

    ;#region è®¾ç½®æœç´¢æ¡†å ä½ç¬¦
    static placeholder {
        set => DllCall("User32.dll\SendMessageW", "Ptr", SearchGui.edit.Hwnd, "Uint", (0x1500 + 1), "Ptr", True, "WStr", value)
    }
    ;#endregion

    ;#region è®¾ç½®æœç´¢æ¨¡å¼ 0: å¯åŠ¨æ¨¡å¼, 1: æ™ºèƒ½æ¨¡å¼
    static _mode := 0
    static mode {
        get => this._mode
        set {
            if (this._mode == value)    ; ä¸éœ€è¦åˆ‡æ¢æ—¶ç›´æ¥è¿”å›
                return
            this._mode := value
            if (this._mode) {    ; IntelligentMode
                this.thumbList[1].Visible := false
                this.thumbList[2].Visible := true
                this.placeholder := "Hi, type something ğŸ˜ƒ"
                StartupMode.autoHideFlag := false    ; å…³é—­å¦ä¸€æ¨¡å¼ä¸‹çš„è‡ªåŠ¨éšè—
                StartupMode.listView.Visible := false    ; éšè—å¦ä¸€æ¨¡å¼çš„åˆ—è¡¨
                IntelligentMode.listView.Visible := true

                this.searchText := this.searchText    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
                IntelligentMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—

            } else {    ; StartupMode
                this.thumbList[1].Visible := true
                this.thumbList[2].Visible := false
                this.placeholder := "Hi, Starter"
                IntelligentMode.autoHideFlag := false    ; å…³é—­å¦ä¸€æ¨¡å¼ä¸‹çš„è‡ªåŠ¨éšè—
                IntelligentMode.listView.Visible := false    ; éšè—å¦ä¸€æ¨¡å¼çš„åˆ—è¡¨
                StartupMode.listView.Visible := true

                this.searchText := this.searchText    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
                StartupMode.autoHideFlag := true    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—
            }
        }
    }
    ;#endregion

    ;#region æœç´¢å†…å®¹
    static _searchText := ""
    static searchText {
        get => this._searchText
        set {
            this._searchText := Value
            if (this.mode) {
                IntelligentMode.search()
            } else {
                StartupMode.search()
            }
        }
    }
    ;#endregion

    ; åˆå§‹åŒ–
    static init() {
        this.gui := Gui("Owner AlwaysOnTop -Resize -Caption", "Starter")
        this.gui.BackColor := "FFFFFF"
        this.gui.SetFont("s20 q5 c333333", "NSimSun")
        this.gui.SetFont(, "é›…ç—-ç®€")    ;ä¼˜å…ˆä½¿ç”¨æ›´å¥½çœ‹çš„å­—ä½“
        this.gui.MarginY := 0

        this.edit := this.gui.AddEdit("x10 y10 h30 w445 -E0x200")
        this.placeholder := "Hi, Starter"
        this.edit.Focus()

        search(edit, info) {    ;é˜²æŠ–
            static f := (*) => this.searchText := this.edit.Value
            SetTimer(f, 0)
            SetTimer(f, -50)
        }
        this.edit.OnEvent("Change", search)    ; æ–‡æœ¬å˜åŠ¨äº‹ä»¶
        this.gui.OnEvent("DropFiles", (g, c, fileList, *) => this.dropAddItem(fileList))    ; æ–‡ä»¶æ‹–å…¥äº‹ä»¶

        this.thumbList := [this.gui.AddPicture("x460 y10 w30 h30", GlobalData.imgDir "\Starter.png")
            , this.gui.AddPicture("x460 y10 w30 h30 Hidden", GlobalData.imgDir "\AI.png")]

        ; å›è½¦è§¦å‘
        this.gui.AddButton("Default Hidden").OnEvent("Click", (*) => this.mode ? IntelligentMode.intelligentRun() : StartupMode.startSelectedFile())

        this.gui.Show("Hide y" Round(A_ScreenHeight / 5))
        StartupMode.init()
        IntelligentMode.init()

        this.hotkeyInit()
    }

    ; æ‹–æ‹½æ·»åŠ 
    static dropAddItem(fileList) {
        for filePath in fileList {
            SplitPath(filePath, , , , &fileNameNoExt)
            GlobalData.startupData.Push([filePath, filePath, 0, fileNameNoExt, ChineseFirstChar(fileNameNoExt)])
        }

        StartupMode.loadImgs(false)    ;è½½å…¥æ–°å›¾ç‰‡ï¼Œä¸é‡æ–°åŠ è½½å…¶ä»–å›¾ç‰‡

        if (SearchGui.mode !== 0)    ; éå¯åŠ¨æ¨¡å¼ä¸‹ï¼Œè½¬ä¸ºå¯åŠ¨æ¨¡å¼
            SearchGui.mode := 0    ; è½¬æ¢å¯åŠ¨æ¨¡å¼æ—¶ä¼šæœç´¢ä¸€æ¬¡
        else
            StartupMode.search()
    }

    ; çƒ­é”®åˆå§‹åŒ–
    static hotkeyInit() {
        capsLockHandler(key) {
            if (KeyWait("CapsLock", "T0.5")) {
                if (A_ThisHotkey = A_PriorHotkey and A_TimeSincePriorHotkey < 300) {
                    StartupMode.showGui()
                }
            } else {
                StartupMode.showGui(GetSelectedText())
                KeyWait("CapsLock")    ;ç­‰å¾…æ¾å¼€
                SetCapsLockState(!GetKeyState("CapsLock", "T"))    ; é•¿æŒ‰ä¼šä¿®æ”¹1æ¬¡çŠ¶æ€ï¼Œæ­¤å¤„ä¿®æ”¹å›åŸæ¥çš„
                ; æœç´¢è·å–é€‰ä¸­å†…å®¹
            }
        }

        ; ä»¥å¯åŠ¨æ¨¡å¼æ¿€æ´»
        Hotkey("~CapsLock", capsLockHandler)

        ; æ ¹æ®æ¨¡å¼å’Œçƒ­é”®å†…å®¹æ‰§è¡Œ
        hotkeyHandler(key) {
            if (this.mode) {
                if (IntelligentMode.hotkeyHandlerMap.Has(key))
                    IntelligentMode.hotkeyHandlerMap[key]()
            } else {
                if (StartupMode.hotkeyHandlerMap.Has(key))
                    StartupMode.hotkeyHandlerMap[key]()
            }
        }

        ;===ä»…å½“çª—å£æ¿€æ´»æ—¶æœ‰æ•ˆ===
        HotIfWinActive("ahk_id " this.gui.Hwnd)

        Hotkey("Tab Up", key => this.mode := !this.mode)
        ; æ¸…ç©ºã€éšè—
        Hotkey("+Esc", hotkeyHandler)
        ; æ–‡ä»¶å¤¹ä¸­æ˜¾ç¤º
        Hotkey("~Left Up", hotkeyHandler)
        ; ä¸Šä¸‹ç§»åŠ¨ç„¦ç‚¹è¡Œ
        Hotkey("Down", hotkeyHandler)
        Hotkey("Up", hotkeyHandler)
        HotIf()
    }

    ; æ ¹æ®æ¨¡å¼éšè—ä¸»ç•Œé¢
    static hideGui() {
        if (this.mode) {
            IntelligentMode.hideGui()
        } else {
            StartupMode.hideGui()
        }
    }
}