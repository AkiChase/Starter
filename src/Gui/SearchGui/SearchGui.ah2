class SearchGui {
    ; static gui := unset
    ; static edit := unset
    static thumbList := []
    static curMode := 0

    static init() {
        this.gui := Gui("Owner AlwaysOnTop -Resize -Caption", "Starter")
        this.gui.BackColor := "FFFFFF"
        this.gui.SetFont("s20 q5 c333333", "NSimSun")
        this.gui.SetFont(, "é›…ç—-ç®€")    ;ä¼˜å…ˆä½¿ç”¨æ›´å¥½çœ‹çš„å­—ä½“
        this.gui.MarginY := 0

        this.edit := this.gui.AddEdit("x10 y10 h30 w445 -E0x200")
        this.setEditplaceholder('Hi, Starter')
        this.edit.Focus()

        search(edit, info) {    ;é˜²æŠ–
            static f := this.search.Bind(this)
            SetTimer(f, 0)
            SetTimer(f, -50)
        }
        this.edit.OnEvent("Change", search)    ; æ–‡æœ¬å˜åŠ¨äº‹ä»¶
        this.gui.OnEvent("DropFiles", StartupMode.dropAddItem)    ; æ–‡ä»¶æ‹–å…¥äº‹ä»¶

        this.thumbList := [this.gui.AddPicture("x460 y10 w30 h30", GlobalData.imgDir "\Starter.png")
            , this.gui.AddPicture("x460 y10 w30 h30 Hidden", GlobalData.imgDir "\AI.png")]

        this.gui.AddButton("Default Hidden").OnEvent("Click", this.enterButtonEvent.Bind(this))

        this.gui.Show("Hide y" Round(A_ScreenHeight / 5))
        StartupMode.init()
        IntelligentMode.init()

        this.hotkeyInit()
    }

    ; å›è½¦è§¦å‘æŒ‰é’®äº‹ä»¶
    static enterButtonEvent(*) {
        if (this.curMode) {
            IntelligentMode.intelligentHandler()
        } else {
            StartupMode.startSelectedFile()
        }
    }

    ; çƒ­é”®åˆå§‹åŒ–
    static hotkeyInit() {
        ; ä»¥å¯åŠ¨æ¨¡å¼æ¿€æ´»
        Hotkey("~CapsLock Up", key => (A_ThisHotkey = A_PriorHotkey and A_TimeSincePriorHotkey < 300) ? StartupMode.showGui() : 0)

        ; æ ¹æ®æ¨¡å¼å’Œçƒ­é”®å†…å®¹æ‰§è¡Œ
        hotkeyHandler(key) {
            if (this.curMode) {
                if (IntelligentMode.hotkeyHandlerMap.Has(key))
                    IntelligentMode.hotkeyHandlerMap[key]()
            } else {
                if (StartupMode.hotkeyHandlerMap.Has(key))
                    StartupMode.hotkeyHandlerMap[key]()
            }
        }

        ;===ä»…å½“çª—å£æ¿€æ´»æ—¶æœ‰æ•ˆ===
        HotIfWinActive("ahk_id " this.gui.Hwnd)

        Hotkey("Tab Up", key => this.toggleMode(!this.curMode))
        ; æ¸…ç©ºã€éšè—
        Hotkey("+Esc", hotkeyHandler)
        ; æ–‡ä»¶å¤¹ä¸­æ˜¾ç¤º
        Hotkey("~Left Up", hotkeyHandler)
        ; ä¸Šä¸‹ç§»åŠ¨ç„¦ç‚¹è¡Œ
        Hotkey("Down", hotkeyHandler)
        Hotkey("Up", hotkeyHandler)
        HotIf()
    }

    ; æŒ‰éœ€åˆ‡æ¢æ¨¡å¼ 0: StartupMode, 1: IntelligentMode
    static toggleMode(mode := 0) {
        if (this.curMode == mode)    ; ä¸éœ€è¦åˆ‡æ¢æ—¶ç›´æ¥åœæ­¢
            return

        this.curMode := mode
        if (mode) {    ; IntelligentMode
            this.thumbList[1].Visible := false
            this.thumbList[2].Visible := true
            this.setEditplaceholder('Hi, type something ğŸ˜ƒ')
            SetTimer(StartupMode.autoHideFunc, 0)    ; å…³é—­å¦ä¸€æ¨¡å¼ä¸‹çš„è‡ªåŠ¨éšè—
            StartupMode.listView.Visible := false    ; éšè—å¦ä¸€æ¨¡å¼çš„åˆ—è¡¨
            IntelligentMode.listView.Visible := true

            this.search()    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
            SetTimer(IntelligentMode.autoHideFunc, 1000)    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—

        } else {    ; StartupMode
            this.thumbList[1].Visible := true
            this.thumbList[2].Visible := false
            this.setEditplaceholder('Hi, Starter')
            SetTimer(IntelligentMode.autoHideFunc, 0)    ; å…³é—­å¦ä¸€æ¨¡å¼ä¸‹çš„è‡ªåŠ¨éšè—
            IntelligentMode.listView.Visible := false    ; éšè—å¦ä¸€æ¨¡å¼çš„åˆ—è¡¨
            StartupMode.listView.Visible := true

            this.search()    ;ä¸æ¸…ç©ºå†…å®¹ç›´æ¥æœç´¢
            SetTimer(StartupMode.autoHideFunc, 1000)    ; å¯åŠ¨æœ¬æ¨¡å¼è‡ªåŠ¨éšè—
        }
    }

    ; æ ¹æ®æ¨¡å¼è¿›è¡Œæœç´¢
    static search() {
        if (this.curMode) {
            IntelligentMode.search()
        } else {
            StartupMode.search()
        }
    }

    ; æ ¹æ®æ¨¡å¼éšè—ä¸»ç•Œé¢
    static hideGui() {
        if (this.curMode) {
            IntelligentMode.hideGui()
        } else {
            StartupMode.hideGui()
        }
    }

    ; è®¾ç½®æœç´¢æ¡†å ä½ç¬¦
    static setEditplaceholder(text) {
        DllCall("User32.dll\SendMessageW", "Ptr", SearchGui.edit.Hwnd, "Uint", (0x1500 + 1), "Ptr", True, "WStr", text)
    }
}